import org.apache.tools.ant.filters.FixCrLfFilter
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "co.riiid.gradle" version "0.4.2"
}

def filteredSourceDir = file("${buildDir}/filtered")

group 'fr.scarex'
version '1.4'

apply plugin: 'java'
apply plugin: 'eclipse'

jar {
    manifest {
        attributes "Main-Class": "fr.scarex.onf.ObfuscatedNameFinder"
    }
}

sourceCompatibility = 1.8

repositories {
    jcenter()
    mavenCentral()
    mavenLocal()
}

task processVersion(type: Copy) {
    from sourceSets.main.java
    into filteredSourceDir
    // Substitute property references in files
    // Use some of the filters provided by Ant
    filter(FixCrLfFilter)
    filter(ReplaceTokens, tokens: ['version': version])
    // Use a closure to filter each line
    filter { String line ->
        line = line.replaceAll('"=onf_version="', '"'+version+'"')
    }
}

task cleanFilteredDir(type: Delete) {
    delete filteredSourceDir
}
processVersion.dependsOn('cleanFilteredDir')

task wrapper(type: Wrapper) {
    gradleVersion = '2.2.1'
}

//force UTF-8 to the compileJava task
compileJava {
    options.encoding = 'UTF-8'
    source = filteredSourceDir
}

compileJava.dependsOn processVersion

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    archives sourceJar
}

tasks.build.dependsOn sourceJar

// Config

ext.privateConfigFile = file 'private.properties'

privateConfigFile.withReader {
    def p = new Properties()
    p.load(it)
    project.ext.privateConfig = new ConfigSlurper().parse p
}


// Github release

def artifactsFiles = new String[0]

configurations.archives.allArtifacts.forEach {
    artifactsFiles += relativePath(it.getFile()).replace("\\", "/")
}

github {
    owner = 'SCAREXgaming'
    repo = rootProject.name
    token = privateConfig.github_token
    tagName = version
    assets = artifactsFiles
}
